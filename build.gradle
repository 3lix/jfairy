apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'

sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
    compile 'org.yaml:snakeyaml:1.13'
    compile 'org.apache.commons:commons-lang3:3.1'
    compile 'com.google.guava:guava:15.0'
    compile 'org.slf4j:slf4j-api:1.7.5'
    testCompile 'ch.qos.logback:logback-classic:1.0.13'
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    testCompile 'cglib:cglib-nodep:2.2.2'
    testCompile 'org.objenesis:objenesis:2.0'
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}


repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes(
                "Application-Name": "Fairyland",
                "Implementation-Title": "Fairyland",
                "Implementation-Version": version,
                "Implementation-Vendor": 'Codearte',
                'Built-Date': new Date(),
                'Built-JDK': System.properties['java.version'],
                'Built-Gradle': gradle.gradleVersion,
                "Built-By": System.properties['user.name'])
    }
}

def pomConfig = {
    url 'https://github.com/Codearte/fairyland'
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
}

publishing {
    publications {
        mavenCustom(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }

            pom.withXml {
                def root = asNode()
                root.appendNode('description', description)
                root.children().last() + pomConfig
            }
        }
    }
    repositories {
        maven {
            url "https://api.bintray.com/maven/codearte/public/fairyland"
            credentials {
                username = bintrayUserName
                password = bintrayApiKey
            }
        }
    }
}

wrapper {
    gradleVersion '1.8'
}
